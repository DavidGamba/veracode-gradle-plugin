import com.veracode.apiwrapper.wrappers.UploadAPIWrapper
import com.veracode.apiwrapper.wrappers.ResultsAPIWrapper
import groovy.json.*

import java.rmi.UnexpectedException

// TODO: refactor as proper Gradle JAR plugin

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath fileTree(dir: 'lib', include: '*.jar')
        classpath group: 'commons-io', name: 'commons-io', version: '2.4'
    }
}

task veracodeAppList << {
    writeXml(
            'build/app-list.xml', loginUpdate().getAppList()
    ).each() { app ->
        out.println app.@app_id + '=' + app.@app_name
    }
}

task veracodeAppInfo << {
    if (!hasArguments(getName(), 'appId')) {
        writeXml(
                'build/app-info.xml', loginUpdate().getAppInfo(appId)
        ).application[0].attributes().each() { k, v ->
            out.println "$k=$v"
        }
    }
}

task veracodeBuildList << {
    if (!hasArguments(getName(), 'appId')) {
        writeXml(
                'build/build-list.xml', loginUpdate().getBuildList(appId)
        ).each() { build ->
            out.println "${build.@build_id}=${build.@version}"
        }
    }
}

task veracodeBuildInfo << {
    if (!hasArguments(getName(), 'appId', 'buildId' + optional)) {
        String xmlResponse
        if (project.hasProperty('buildId')) {
            xmlResponse = loginUpdate().getBuildInfo(appId, buildId)
        } else {
            xmlResponse = loginUpdate().getBuildInfo(appId)
        }
        Node buildInfo = writeXml('build/build-info.xml', xmlResponse) // need to print twice, so assign var
        println '[Build]'
        buildInfo.build[0].attributes().each() { k, v ->
            out.println "\t$k=$v"
        }
        println '[Analysis Unit]'
        buildInfo.build[0].children()[0].attributes().each { k, v ->
            out.println "\t$k=$v"
        }
    }
}

task veracodeFileList << {
    if (!hasArguments(getName(), 'appId')) {
        String xmlResponse
        if (project.hasProperty('buildId')) {
            xmlResponse = loginUpdate().getFileList(appId, buildId)
        } else {
            xmlResponse = loginUpdate().getFileList(appId)
        }
        Node filelist = writeXml('build/file-list.xml', xmlResponse)
        filelist.each() { file ->
            println "${file.@file_name}=${file.@file_status}"
        }
        println '\tTotal=' + filelist.children().size()
    }
}

task veracodePreScanResults << {
    if (!hasArguments(getName(), 'appId', 'buildId' + optional)) {
        String xmlResponse
        if (project.hasProperty('buildId')) {
            xmlResponse = loginUpdate().getPreScanResults(appId, buildId)
        } else {
            xmlResponse = loginUpdate().getPreScanResults(appId)
        }

        Node result = writeXml('build/pre-scan-results.xml', xmlResponse)
        if (result.name().equals('error')) {
            println result.text()
        } else {
            println 'Pre-scan completed'
        }
    }
}

task veracodeScanResults << {
    if (!hasArguments(getName(), 'buildId')) {
        String xmlResponse = loginResults().detailedReport(buildId)
        writeXml('build/scan-results.xml', xmlResponse)
    }
}

task veracodeScanResultsInCsv(dependsOn: 'veracodeScanResults') << {
    File csvFile = new File('build/scan-results.csv')
    csvFile.newWriter()
    csvFile << ["Issue Id",
            "Severity",
            "Exploit Level",
            "CWE Id",
            "CWE Name",
            "Module",
            "Source",
            "Source File Path",
            "Line",
            "Remediation Status",
            "Mitigation Status",
            "Mitigation Action",
            "Mitigation Description",
            "Mitigation Date"].join(",") + "\n"

    readXml('build/scan-results.xml').severity.each() { severity ->
        severity.category.each() { category ->
            category.cwe.each() { cwe ->
                cwe.staticflaws.flaw.each() { flaw ->
                    def row = [flaw.@issueid,
                            flaw.@severity,
                            flaw.@exploitLevel,
                            cwe.@cweid,
                            cwe.@cwename,
                            flaw.@module,
                            flaw.@sourcefile,
                            flaw.@sourcefilepath,
                            flaw.@line,
                            flaw.@remediation_status,
                            flaw.@mitigation_status_desc,
                            flaw.mitigations?.mitigation[0]?.@action,
                            flaw.mitigations?.mitigation[0]?.@description,
                            flaw.mitigations?.mitigation[0]?.@date]
                            .collect { '"' + (it == null ? "" : it.replace('"', '""')) + '"' }
                    def rowString = row.join(',')
                    csvFile << rowString + "\n"
                }
            }
        }
    }
}

task veracodeCreateBuild << {
    if (!hasArguments(getName(), 'appId', 'version')) {
        Node buildInfo = writeXml('build/create-build.xml', loginUpdate().createBuild(appId, version))
        if (buildInfo.name().equals('error')) {
            println buildInfo.text()
        } else {
            println '[Build]'
            buildInfo.build[0].attributes().each() { k, v ->
                out.println "\t$k=$v"
            }
            println '[Analysis Unit]'
            buildInfo.build[0].children()[0].attributes().each { k, v ->
                out.println "\t$k=$v"
            }
        }
    }
}

task generateToUpload << {
    List<File> files = []

    if (!hasArguments(getName(), 'dir', 'force' + optional)) {

        // prevent uploading if "to-upload" is not empty unless user force
        if (!project.hasProperty('force')) {
            File toUploadDir = new File('build/to-upload')
            if (toUploadDir.isDirectory() && toUploadDir.list().size() > 0) {
                // to-upload is not empty
                println 'Directory "build/to-upload" is not empty. Cannot proceed.'
                return
            }
        }

        // get files to process
        new File((String) dir).eachFileRecurse() { file ->
            if (file.isFile() && file.name.toLowerCase().endsWith(".jar")) {
                files.add file
            }
        }

        // copy to build directory
        for (File file : files) {
            copy {
                from file
                into 'build/to-upload'

                // strip timestamp
                rename { String fileName ->
                    fileName.replaceAll(/(.+)-\d{8}.\d{6}-\d+/, '$1')
                }
            }
        }

        println "${files.size()} file(s) copied to build/to-upload"

    }
}

task veracodeUpload << {
    String xmlResponse = ''
    UploadAPIWrapper update = loginUpdate()

    if (!hasArguments(getName(), 'appId')) {
        // upload each file in build/to-upload
        new File('build/to-upload').eachFile() { file ->
            xmlResponse = update.uploadFile(appId, file.absolutePath)
            delete file.absolutePath
            println "Processed $file.name"
        }

        // write output of last upload
        writeXml('build/upload-file.xml', xmlResponse)

        println 'Check build/upload-file.xml for status of uploaded files.'
    }
}

task veracodePreScan << {
    if (!hasArguments(getName(), 'appId')) {
        writeXml('build/pre-scan.xml', loginUpdate().beginPreScan(appId))
        println 'Check build/pre-scan.xml for response status.'
    }
}

task veracodeScan << {

    if (!hasArguments(getName(), 'appId')) {
        def moduleIds = []
        def whiteList = readListFromFile(new File("src/apps/${appId}/modules-whitelist.txt"))
        readXml('build/pre-scan-results.xml').each() { module ->
            if (whiteList.contains(module.@name)) {
                moduleIds << module.@id
            }
        }
        println "Modules in whitelist: ${whiteList.size()}"
        println "Modules selected: ${moduleIds.size()}"

        writeXml('build/scan.xml', loginUpdate().beginScan(appId, moduleIds.join(","), 'false'))
    }
}

task veracodeRemoveFile << {
    // TODO: this is not tested
    if (!hasArguments(getName(), 'appId', 'fileId')) {
        writeXml('build/remove-file.xml', loginUpdate().removeFile(appId, fileId))
    }
}

task veracodeDeleteBuild << {
    // Removes the most recent build, even those that have their scan completed!
    if (!hasArguments(getName(), 'appId')) {
        writeXml('build/delete-build.xml', loginUpdate().deleteBuild(appId))
    }
}

task preScanModuleList() << {
    // TODO: -PonlyFatalErrors
    def blacklist = [:]
    def whitelist = [:]
    readXml('build/pre-scan-results.xml').each() { module ->
        module.@has_fatal_errors == 'true' ? blacklist.put(module.@id, module.@name) : whitelist.put(module.@id, module.@name)
    }

    blacklist.each() { println 'Blacklist => ' + it }
    whitelist.each() { println 'Whitelist => ' + it }

    def blacklistedJars = ''
    def whitelistedJars = ''
    blacklist.values().each() { blacklistedJars += it + '\n' }
    whitelist.values().each() { whitelistedJars += it + '\n' }
    GFileUtils.writeStringToFile(new File('build/blacklist.txt'), blacklistedJars)
    GFileUtils.writeStringToFile(new File('build/whitelist.txt'), whitelistedJars)
}

/**
 * This task ensures all modules in pre-scan results are accounted for within the white/black list of modules in
 * "src/apps/${appId}/modules-*.txt".
 */
task preScanModuleVerify() << {
    if (!hasArguments(getName(), 'appId')) {

        println "Verifying against modules list modules-*.txt..."
        println ""

        def blackListErr = readListFromFile(new File("src/apps/${appId}/modules-blacklist-error.txt"))
        def blackListExt = readListFromFile(new File("src/apps/${appId}/modules-blacklist-external.txt"))
        def blackListInt = readListFromFile(new File("src/apps/${appId}/modules-blacklist-internal.txt"))
        def whiteList = readListFromFile(new File("src/apps/${appId}/modules-whitelist.txt"))

        def allList = blackListErr + blackListExt + blackListInt + whiteList
        allList = allList.sort()

        println "modules-blacklist-error = ${blackListErr.size()}"
        println "modules-blacklist-external = ${blackListExt.size()}"
        println "modules-blacklist-internal = ${blackListInt.size()}"
        println "modules-whitelist = ${whiteList.size()}"
        println "all lists total = ${allList.size()}"
        println ""

        def notFoundList = new ArrayList<String>()
        def fatalErrorList = new ArrayList<String>()
        readXml('build/pre-scan-results.xml').each() { module ->
            if (!allList.contains(module.@name)) {
                notFoundList << module.@name
            }

            if (module.@has_fatal_errors.equals("true") && whiteList.contains(module.@name)) {
                fatalErrorList << module.@name
            }
        }

        if (notFoundList.isEmpty() && fatalErrorList.isEmpty()) {
            println "Prescan module verification success."
        } else {
            println "Prescan module verification failed. DO NOT PROCEED WITH VERACODE SCAN!"
            if (!notFoundList.isEmpty()) {
                println "Module not found in all list:"
                notFoundList.each { notFoundModule ->
                    println "${notFoundModule}"
                }
            }

            if (!fatalErrorList.isEmpty()) {
                println "Module in fatal error but in whitelist:"
                fatalErrorList.each { fatalErrorModule ->
                    println "${fatalErrorModule}"
                }
            }
        }

    }
}

/**
 * Report flaws by teams as described in src/teams.json
 */
task reportFlawsByTeam() << {
    if (!hasArguments(getName(), 'appId', 'mode')) {
        def teams = new JsonSlurper().parse(new FileReader("src/apps/$appId/teams.json")).teams
        int count = 0

        readXml('build/scan-results.xml').severity.each() { severity ->
            severity.category.each() { category ->
                category.cwe.each() { cwe ->
                    cwe.staticflaws.flaw.each() { flaw ->

                        if (mode == 'verbose') {
                            // verbose report
                            def row = [findTeam(teams, flaw),
                                    flaw.@issueid,
                                    flaw.@severity,
                                    flaw.@exploitLevel,
                                    cwe.@cweid,
                                    cwe.@cwename,
                                    flaw.@module,
                                    flaw.@sourcefile,
                                    flaw.@sourcefilepath,
                                    flaw.@line,
                                    flaw.@remediation_status,
                                    flaw.@mitigation_status_desc,
                                    flaw.mitigations?.mitigation[0]?.@action,
                                    flaw.mitigations?.mitigation[0]?.@description,
                                    flaw.mitigations?.mitigation[0]?.@date]
                                    .collect { '"' + (it == null ? "" : it.replace('"', '""')) + '"' }
                            println row.join(',')
                            count++
                        } else if (mode == 'actionSummary') {
                            // only flaws that require action
                            if (!['Mitigation Proposed'].contains(flaw.@mitigation_status_desc) &&
                                    !['Fixed'].contains(flaw.@remediation_status)
                            ) {
                                def row = [findTeam(teams, flaw),
                                        flaw.@issueid,
                                        cwe.@cwename,
                                        flaw.@module,
                                        flaw.@remediation_status,
                                        flaw.@mitigation_status_desc]
                                        .collect { '"' + (it == null ? "" : it.replace('"', '""')) + '"' }
                                println row.join(',')
                                count++


                            }
                        } else if (mode == 'action') {
                            // only flaws that require action
                            if (!['Mitigation Proposed'].contains(flaw.@mitigation_status_desc) &&
                                    !['Fixed'].contains(flaw.@remediation_status)
                            ) {
                                def row = [findTeam(teams, flaw),
                                        flaw.@issueid,
                                        cwe.@cwename,
                                        flaw.@module,
                                        flaw.@sourcefile,
                                        flaw.@sourcefilepath,
                                        flaw.@remediation_status,
                                        flaw.@mitigation_status_desc]
                                        .collect { '"' + (it == null ? "" : it.replace('"', '""')) + '"' }
                                println row.join(',')
                                count++
                            }
                        } else {
                            println 'Unsupported mode.'
                        }

                    }
                }
            }
        }
        println "Total flaw count: $count."

    }
}

// === utility methods ===

UploadAPIWrapper loginUpdate() {
    UploadAPIWrapper update = new UploadAPIWrapper()
    update.setUpCredentials username, password
    return update
}

ResultsAPIWrapper loginResults() {
    ResultsAPIWrapper results = new ResultsAPIWrapper()
    results.setUpCredentials username, password
    return results
}

def writeXml(String filename, String content) {
    GFileUtils.writeFile(content, new File(filename))
    new XmlParser().parseText(content)
}

def readXml(String filename) {
    new XmlParser().parseText(GFileUtils.readFile(new File(filename)))
}

List readListFromFile(File file) {

    def set = new HashSet<Set>();
    file.eachLine { line ->
        if (set.contains(line)) {
            println "ERROR: duplicate line: [$line]"
        }
        set.add(line)
    }
    return new ArrayList<String>(set)
}

String findTeam(teams, flaw) {
    // TODO: support source path exclusions? should not be part of team?

    for (def team : teams) {
        // see if flaw's module is excluded from this team
        boolean excludeFromTeam = false;

        for (def moduleExclusion : team.moduleExclusions) {
            if (flaw.@module.contains(moduleExclusion)) {
                excludeFromTeam = true;
                break;
            }
        }

        // this flaw's module is excluded, check next team
        if (excludeFromTeam) {
            continue;
        }

        // see if this flaw's module is assign to this team
        for (def module : team.modules) {
            if (flaw.@module.contains(module)) {
                return team.name
            }
        }
    }

    // no match
    throw new UnexpectedException("Unable to resolve team")
}

boolean hasArguments(String taskName, String... arguements) {
    StringBuilder outputMessage = new StringBuilder("Usage: gradle ${taskName}")
    boolean missingArguement = false

    def parameters = [
    'appId': '-PappId=123',
    'buildId': '-PbuildId=123',
    'version': '-Pversion=xxx',
    'dir': '-Pdir=xxx',
    'force': '-Pforce',
    'fileId': '-PfileId=123',
    'mode': '-Pmode=action|actionSummary|verbose']

    arguements.each() {
        if (it.endsWith(optional)) {
            String originalArguement = it.substring(0, it.length() - optional.length())
            outputMessage.append(' [')
            outputMessage.append(parameters.get(originalArguement))
            outputMessage.append(']')
        } else {
            outputMessage.append(' ')
            outputMessage.append(parameters.get(it))
            if (!project.hasProperty(it)) {
                missingArguement = true
            }
        }
    }

    if (missingArguement) {
        println outputMessage.toString()
    }

    return missingArguement
}
